apply plugin: 'maven-publish'

def nexus_username = System.getenv('NEXUS_USERNAME') ?: NEXUS_USERNAME
def nexus_password = System.getenv('NEXUS_PASSWORD') ?: NEXUS_PASSWORD

group = GROUP

project.afterEvaluate {
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
//        classifier "sources"
    }

    task javadocJar(type: Jar, dependsOn: 'dokkaJavadoc') {
        from "$buildDir/dokka/javadoc"
//        classifier = 'javadoc'
    }

    publishing {
        publications {
            library(MavenPublication) {
                version android.defaultConfig.versionName + "-SNAPSHOT"

                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = POM_NAME
                    description = POM_DESCRIPTION
                    packaging = POM_PACKAGING
                    url = POM_URL
                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                            distribution = POM_LICENCE_DIST
                        }
                    }
                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            url = POM_DEVELOPER_URL
                        }
                    }
                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        if (it.version == "unspecified" && it.group == GROUP) {
                            // Assume dependent modules in the project use the same version
                            dependencyNode.appendNode('version', version)
                        } else {
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url "https://artifacts.alfresco.com/nexus/content/repositories/snapshots/"
                credentials {
                    username nexus_username
                    password nexus_password
                }
            }
        }
    }
}
