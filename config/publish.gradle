apply plugin: 'maven-publish'

def nexus_username = System.getenv('NEXUS_USERNAME') ?: NEXUS_USERNAME
def nexus_password = System.getenv('NEXUS_PASSWORD') ?: NEXUS_PASSWORD
def maven_group    = 'com.alfresco.android'

group = GROUP

project.afterEvaluate {
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier "sources"
    }

    task javadocJar(type: Jar, dependsOn: 'dokkaJavadoc') {
        from "$buildDir/dokka/javadoc"
        classifier = 'javadoc'
    }

    publishing {
        publications {
            library(MavenPublication) {
                version android.defaultConfig.versionName + "-SNAPSHOT"

                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = POM_NAME
                    description = POM_DESCRIPTION
                    packaging = POM_PACKAGING
                    url = POM_URL
                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                            distribution = POM_LICENCE_DIST
                        }
                    }
                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            url = POM_DEVELOPER_URL
                        }
                    }
                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    ['implementation', 'compile'].each {
                        getConfigurations().getByName(it).allDependencies.withType(ModuleDependency) { ModuleDependency dp ->
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            def dependencyGroup = dp.group == defaultGroupId ? maven_group : dp.group
                            dependencyNode.appendNode('groupId', dependencyGroup)
                            dependencyNode.appendNode('artifactId', dp.name)
                            dependencyNode.appendNode('version', dp.version)

                            // for exclusions
                            if (dp.excludeRules.size() > 0) {
                                def exclusions = dependencyNode.appendNode('exclusions')
                                dp.excludeRules.each { ExcludeRule ex ->
                                    def exclusion = exclusions.appendNode('exclusion')
                                    exclusion.appendNode('groupId', ex.group)
                                    exclusion.appendNode('artifactId', ex.module)
                                }
                            }
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url "https://artifacts.alfresco.com/nexus/content/repositories/snapshots/"
                credentials {
                    username nexus_username
                    password nexus_password
                }
            }
        }
    }
}
