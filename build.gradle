import org.jetbrains.dokka.gradle.DokkaTask

buildscript {
    repositories {
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.0'
        classpath libs.kotlin.gradlePlugin
        classpath libs.kotlin.serialization.plugin

        classpath libs.swaggerCodegen

        classpath libs.dokka
        classpath libs.spotless
        classpath libs.gradleVersionsPlugin
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.0'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    afterEvaluate {
        if(it.hasProperty('android')) {
            android {
                compileSdkVersion 33

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 33
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_18
                    targetCompatibility JavaVersion.VERSION_18
                }

                sourceSets['main'].java.srcDir('src/main/kotlin')
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_18.toString()
        }
    }

    tasks.withType(DokkaTask).configureEach {
        dokkaSourceSets.configureEach {
            reportUndocumented.set(true)
            skipEmptyPackages.set(true)
            skipDeprecated.set(true)
            jdkVersion.set(18)

            // Add Android SDK packages
            noAndroidSdkLink.set(false)

            // AndroidX docs
            externalDocumentationLink {
                url.set(new URL("https://developer.android.com/reference/"))
                packageListUrl.set(new URL("https://developer.android.com/reference/androidx/package-list"))
            }
        }
    }

    // Exclude 'content' module as it contains generated code
    if (it.name != 'content') {
        apply from: "$rootDir/config/spotless.gradle"
    }
}

apply from: "$rootDir/config/dependencyUpdates.gradle"
