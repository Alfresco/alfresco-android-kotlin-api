buildscript {
    repositories {
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath libs.android.gradlePlugin
        classpath libs.kotlin.gradlePlugin
        classpath libs.kotlin.serialization.plugin

        classpath libs.swaggerCodegen

        classpath libs.dokka
        classpath libs.spotless
        classpath libs.gradleVersionsPlugin
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    afterEvaluate {
        if(it.hasProperty('android')) {
            android {
                compileSdkVersion 32

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 32
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                sourceSets['main'].java.srcDir('src/main/kotlin')
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8.toString()
        }
    }

    tasks.withType(org.jetbrains.dokka.gradle.DokkaTask).configureEach {
        dokkaSourceSets.configureEach {
            reportUndocumented.set(true)
            skipEmptyPackages.set(true)
            skipDeprecated.set(true)
            jdkVersion.set(8)

            // Add Android SDK packages
            noAndroidSdkLink.set(false)

            // AndroidX docs
            externalDocumentationLink {
                url.set(new URL("https://developer.android.com/reference/"))
                packageListUrl.set(new URL("https://developer.android.com/reference/androidx/package-list"))
            }
        }
    }

    // Exclude 'content' module as it contains generated code
    if (it.name != 'content') {
        apply from: "$rootDir/config/spotless.gradle"
    }
}

apply from: "$rootDir/config/dependencyUpdates.gradle"
