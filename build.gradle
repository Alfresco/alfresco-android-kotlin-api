import com.alfresco.buildsrc.Libs
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath Libs.AndroidTools.gradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.spotless
    }
}

allprojects {
    repositories {
        maven { url "https://artifacts.alfresco.com/nexus/content/groups/public" }
        mavenLocal()
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate {
        if(it.hasProperty('android')) {
            android {
                compileSdkVersion 29

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 29
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                sourceSets['main'].java.srcDir('src/main/kotlin')
            }
        }
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8.toString()
        }
    }

    // Exclude 'content' module as it contains generated code
    if (it.name != "content") {
        apply from: "$project.rootDir/config/spotless.gradle"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
