def nexus_username = System.getenv('NEXUS_USERNAME') ?: NEXUS_USERNAME
def nexus_password = System.getenv('NEXUS_PASSWORD') ?: NEXUS_PASSWORD

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                setGroupId "com.alfresco.dbp.android"
                setArtifactId artifactId
                version android.defaultConfig.versionName+"-SNAPSHOT"

                artifact bundleReleaseAar

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.getDependencies().withType(ModuleDependency) { ModuleDependency dp ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dp.group)
                        dependencyNode.appendNode('artifactId', dp.name)
                        dependencyNode.appendNode('version', dp.version)

                        // for exclusions
                        if (dp.excludeRules.size() > 0) {
                            def exclusions = dependencyNode.appendNode('exclusions')
                            dp.excludeRules.each { ExcludeRule ex ->
                                def exclusion = exclusions.appendNode('exclusion')
                                exclusion.appendNode('groupId', ex.group)
                                exclusion.appendNode('artifactId', ex.module)
                            }
                        }
                    }
                    configurations.api.getDependencies().withType(ModuleDependency) { ModuleDependency dp ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dp.group)
                        dependencyNode.appendNode('artifactId', dp.name)
                        dependencyNode.appendNode('version', dp.version)

                        // for exclusions
                        if (dp.excludeRules.size() > 0) {
                            def exclusions = dependencyNode.appendNode('exclusions')
                            dp.excludeRules.each { ExcludeRule ex ->
                                def exclusion = exclusions.appendNode('exclusion')
                                exclusion.appendNode('groupId', ex.group)
                                exclusion.appendNode('artifactId', ex.module)
                            }
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url "https://artifacts.alfresco.com/nexus/content/repositories/internal-snapshots/"
                credentials {
                    username nexus_username
                    password nexus_password
                }
            }
        }
    }
}
